ShortFormApplicationService = ($localStorage, $translate, $http, Upload, uuid, ListingService) ->
  Service = {}
  Service.listing = ListingService.listing
  Service.form = {}
  Service.applicationDefaults =
    id: null
    lotteryNumber: null
    status: null
    applicationSubmittedDate: null
    surveyComplete: false
    applicationSubmissionType: "Electronic"
    applicant:
      home_address: { address1: null, address2: "", city: null, state: null, zip: null }
      language: "English"
      phone: null
      mailing_address: { address1: null, address2: "", city: null, state: null, zip: null }
      gender: {}
    alternateContact:
      language: "English"
      mailing_address: { address1: null, address2: "", city: null, state: null, zip: null }
    householdMembers: []
    preferences:
      liveInSf: null
      workInSf: null
      neighborhoodResidence: null
      liveInSf_file: null
      workInSf_file: null
      neighborhoodResidence_file: null
      liveInSf_file_loading: null
      workInSf_file_loading: null
      neighborhoodResidence_file_loading: null
      liveInSf_file_error: null
      workInSf_file_error: null
      neighborhoodResidence_file_error: null
    householdIncome: { incomeTotal: 0, incomeTimeframe: 'per_year' }
    completedSections:
      Intro: false
      You: false
      Household: false
      Status: false
      Income: false
    # as you proceed through each page, validatedForms will store:
    #  [pagename]: T/F
    # to indicate if that form was left valid or invalid
    validatedForms:
      You: {}
      Household: {}
      Status: {}
      Income: {}
      Review: {}

  Service.current_id = 0
  Service.session_uid = uuid.v4()
  Service.userkey = uuid.v4()

  Service.resetUserData = () ->
    # even though this looks commented out, it still has an effect in ERB land
    # <% if Rails.env.development? %> # -------------
    $localStorage.application ?= Service.applicationDefaults
    Service.application = $localStorage.application
    Service.application.preferences = angular.copy(Service.applicationDefaults.preferences)
    Service.application.id = null
    Service.application.lotteryNumber = null
    Service.application.status = null
    Service.application.applicationSubmittedDate = null
    # <% else %> # -------------
    Service.application = angular.copy(Service.applicationDefaults)
    # <% end %> # -------------
    Service.applicant = Service.application.applicant
    Service.preferences = Service.application.preferences
    Service.alternateContact = Service.application.alternateContact
    Service.householdMember = {}
    Service.householdMembers = Service.application.householdMembers

  Service.resetUserData()

  Service.completeSection = (section) ->
    Service.application.completedSections[section] = true

  Service.userCanAccessSection = (name, state = null) ->
    # user can't access later sections yet when on the welcome page
    return false if state and Service.isWelcomePage(state)
    completed = Service.application.completedSections
    validated = Service.application.validatedForms
    switch name
      when 'You'
        true
      when 'Household'
        completed.Intro &&
        completed.You &&
        # make sure all validatedForms in previous section == true
        _.every(validated['You'], (i) -> i)
      when 'Status'
        Service.userCanAccessSection('Household') &&
        completed.Household &&
        # make sure all validatedForms in previous section == true
        _.every(validated['Household'], (i) -> i)
      when 'Income'
        Service.userCanAccessSection('Status') &&
        completed.Status &&
        # make sure all validatedForms in previous section == true
        _.every(validated['Status'], (i) -> i)
      when 'Review'
        Service.userCanAccessSection('Income') &&
        completed.Income &&
        # make sure all validatedForms in previous section == true
        _.every(validated['Income'], (i) -> i)
      else
        false

  Service.copyHomeToMailingAddress = () ->
    angular.copy(Service.applicant.home_address, Service.applicant.mailing_address)

  Service.validMailingAddress = () ->
    !! (Service.applicant.mailing_address.address1 &&
        Service.applicant.mailing_address.city &&
        Service.applicant.mailing_address.state &&
        Service.applicant.mailing_address.zip)

  Service.missingPrimaryContactInfo = () ->
    missingInfo = []
    if !Service.applicant.phone
      missingInfo.push("Phone")
    if !Service.applicant.email
      missingInfo.push("Email")
    if !Service.validMailingAddress()
      missingInfo.push("Address")
    return missingInfo

  Service.clearPhoneData = (type) ->
    if type == 'alternate'
      Service.applicant.noPhone = false
      Service.applicant.alternatePhone = null
      Service.applicant.alternatePhoneType = null
    else if type == 'phone'
      Service.applicant.additionalPhone = false
      Service.applicant.phone = null
      Service.applicant.phoneType = null

  Service.clearAlternateContactDetails = ->
    clearedData = { alternateContactType: 'None' }
    angular.copy(clearedData, Service.application.alternateContact)

  Service._nextId = ->
    if Service.householdMembers.length > 0
      max_id = _.maxBy(Service.householdMembers, 'id').id
    else
      max_id = Service.current_id
    Service.current_id = max_id + 1

  Service.addHouseholdMember = (householdMember) ->
    if !householdMember.id
      householdMember.id = Service._nextId()
      Service.householdMembers.push(angular.copy(householdMember))
    else
      # update existing householdMember
      angular.copy(householdMember, Service.householdMember)
    Service.householdMember = {}
    Service.invalidateHouseholdForm()

  Service.resetHouseholdmember = () ->
    Service.householdMember = {}

  Service.getHouseholdMember = (id) ->
    Service.householdMember = _.find(Service.householdMembers, {id: parseInt(id)})

  Service.cancelHouseholdMember = ->
    householdMembers = Service.householdMembers.filter (m) ->
      (m != Service.householdMember && m.id != Service.householdMember.id)
    # clear out preferences where this householdMember was selected
    full_name = "#{Service.householdMember.firstName} #{Service.householdMember.lastName}"
    # search through all xxx_household_member items in preferences matching the full name
    _.each Service.preferences, (v,k) ->
      if k.indexOf('_household_member') > 0 and v == full_name
        preference = k.split('_household_member')[0]
        Service.preferences[preference] = false
        Service.preferences[k] = null
        if Service.preferences["#{preference}_proof_option"]
          Service.preferences["#{preference}_proof_option"] = null
        if Service.hasPreferenceFile("#{preference}_proof_file")
          Service.deletePreferenceFile("#{preference}_proof_file")

    # persist the changes to Service.householdMembers / $localStorage
    Service.householdMember = {}
    angular.copy(householdMembers, Service.householdMembers)
    Service.invalidateHouseholdForm()

  Service.checkHouseholdEligiblity = (listing) ->
    params =
      listing_id: listing.Id,
      eligibility:
        householdsize: Service.application.householdMembers.length + 1
        incomelevel: Service._calculateHouseholdIncome()
    $http.post("/api/v1/short-form/validate-household", params).success((data, status, headers, config) ->
      data
    ).error( (data, status, headers, config) ->
      return
    )

  Service._calculateHouseholdIncome = () ->
    income = Service.application.householdIncome
    if income.incomeTimeframe == 'per_year'
      income.incomeTotal
    else if income.incomeTimeframe == 'per_month'
      income.incomeTotal * 12

  Service.resetGenderOptions = (user, option) ->
    toggle = !user.gender[option]
    user.gender = {}
    user.genderOther = null
    user.gender[option] = toggle

  ### Proof of Preferences ###
  Service.refreshLiveWorkPreferences = () ->
    Service._updatePreference("liveInSf", Service.liveInSfMembers())
    Service._updatePreference("workInSf", Service.workInSfMembers())

  Service.liveInSfMembers = () ->
    applicantLivesInSf = _.lowerCase(Service.applicant.home_address.city) == 'san francisco'
    liveInSfMembers = Service.application.householdMembers.filter (member) ->
      if member.hasSameAddressAsApplicant == "No"
        _.lowerCase(member.home_address.city) == 'san francisco'
      else
        member.hasSameAddressAsApplicant == "Yes" && applicantLivesInSf
    liveInSfMembers.push(Service.applicant) if applicantLivesInSf
    return liveInSfMembers

  Service.workInSfMembers = () ->
    workInSfMembers = Service.application.householdMembers.filter (member) ->
      member.workInSf == "Yes"
    applicantWorksInSf = Service.applicant.workInSf == "Yes"
    workInSfMembers.push(Service.applicant) if applicantWorksInSf
    return workInSfMembers

  Service.neighborhoodResidenceMembers = () ->
    # keeping this code here for now so that general-lottery-notice can be accessed for review
    # TODO: Update this code when charlie's api is ready
    # household = angular.copy(Service.application.householdMembers)
    # household.push(Service.applicant)
    # return household
    return []

  Service.preferencesApplyForHousehold = ->
    liveInSfEligible = Service.liveInSfMembers().length > 0
    workInSfEligible = Service.workInSfMembers().length > 0
    neighborhoodResidenceEligible =  Service.neighborhoodResidenceMembers().length > 0
    displacedOrCertOfPreference = Service.preferences.displaced || Service.preferences.certOfPreference
    return (liveInSfEligible || workInSfEligible || neighborhoodResidenceEligible || displacedOrCertOfPreference)

  Service._updatePreference = (preference, eligibleMembers) ->
    # only need to update if nobody is eligible
    Service.preferences[preference] = false if _.isEmpty(eligibleMembers)

  Service.onExit = (e) ->
    e.returnValue = $translate.instant('T.ARE_YOU_SURE_YOU_WANT_TO_LEAVE')

  Service.isWelcomePage = (state) ->
    !!state.name.match(/short-form-welcome\./)

  Service.isShortFormPage = (state) ->
    !!state.name.match(/short-form-application\./)

  Service.checkFormState = (stateName, section) ->
    if Service.form.applicationForm
      stateName = stateName.replace(/dahlia.short-form-(welcome|application)\./, "")
      # special case for household-member-form
      return if stateName.match(/household-member-form/)
      Service.application.validatedForms[section.name][stateName] = Service.form.applicationForm.$valid

  Service.authorizedToProceed = (toState, fromState, toSection) ->
    return true unless toState && fromState
    return false unless Service.userCanAccessSection(toSection.name)
    # they're "jumping ahead" if they're not coming from a short form page or create-account
    # and they're trying to go to a short form section ahead of "name" (1st page)
    jumpAhead = Service.isShortFormPage(toState) &&
                !Service.isShortFormPage(fromState) &&
                toState.name != 'dahlia.short-form-application.name'
    return !jumpAhead

  Service.isLeavingShortForm = (toState, fromState) ->
    # leaving means we're going from short form to a non-short form page
    Service.isShortFormPage(fromState) && !Service.isShortFormPage(toState)

  Service.invalidateIncomeForm = ->
    Service.application.completedSections['Income'] = false
    Service.application.validatedForms['Income']['income'] = false

  Service.invalidateHouseholdForm = ->
    Service.application.completedSections['Household'] = false
    Service.application.completedSections['Status'] = false
    Service.application.completedSections['Income'] = false

  Service.hasPreferenceFile = (fileType) ->
    Service.preferences[fileType] && !Service.preferenceFileIsLoading(fileType)

  Service.deletePreferenceFile = (prefType) ->
    fileType = "#{prefType}_proof_file"
    params =
      uploaded_file:
        session_uid: Service.session_uid
        userkey: Service.userkey
        preference: prefType
    $http.delete('/api/v1/short-form/proof', {
      data: params,
      headers: {
        'Content-Type': 'application/json'
      },
    }).success((data, status, headers, config) ->
      Service.preferences[fileType] = null
      # console.log(data)
    ).error( (data, status, headers, config) ->
      # console.warn('error')
      return
    )

  Service.preferenceFileError = (fileType) ->
    !! Service.preferences["#{fileType}_error"]

  Service.preferenceFileIsLoading = (fileType) ->
    !! Service.preferences["#{fileType}_loading"]

  Service.uploadProof = (file, prefType) ->
    fileType = "#{prefType}_proof_file"
    if (!file)
      Service.preferences["#{fileType}_error"] = true
      return
    Service.preferences["#{fileType}_loading"] = true
    Upload.upload(
      url: '/api/v1/short-form/proof'
      method: 'POST'
      data:
        uploaded_file:
          file: file
          session_uid: Service.session_uid
          userkey: Service.userkey
          preference: prefType
    ).then( ((resp) ->
      Service.preferences["#{fileType}_loading"] = false
      Service.preferences["#{fileType}_error"] = false
      Service.preferences["#{fileType}"] = resp.data
    ), ((resp) ->
      # error handler
      Service.preferences["#{fileType}_loading"] = false
      Service.preferences["#{fileType}_error"] = true
    ))

  Service.submitApplication = (options={}) ->
    if options.draft
      Service.application.status = 'draft'
    else
      Service.application.status ='submitted'
      Service.application.applicationSubmittedDate = moment().format('YYYY-MM-DD')

    params =
      application: Service._formatApplication(Service.listing.Id)
    if options.attachToAccount
      params.temp_session_id = Service.session_uid + Service.userkey
    $http.post('/api/v1/short-form/submit-application', params).success((data, status, headers, config) ->
      if data.lotteryNumber
        Service.application.lotteryNumber = data.lotteryNumber
        Service.application.id = data.id
    ).error( (data, status, headers, config) ->
      alert("Error with submission: #{data.error}")
      return
    )

  Service._formatApplication = (listingId) ->
    application = angular.copy(Service.application)
    application.listingID = listingId
    application = Service._formatDOB(application)
    application = Service._formatAddress(application, 'applicant', 'home_address')
    application = Service._formatAddress(application, 'applicant', 'mailing_address')
    if application.alternateContact.alternateContactType == 'None'
      delete application.alternateContact
    else
      application = Service._formatAddress(application, 'alternateContact', 'mailing_address')
    application = Service._formatMemberAddress(application)
    application = Service._formatMemberDOB(application)
    application = Service._formatPreferences(application)
    application = Service._formatGenderOptions(application)
    application = Service._formatReferrals(application)
    application = Service._formatTerms(application)
    application = Service._formatIncome(application)
    application = Service._formatBooleans(application)
    application = Service._renameApplicant(application)
    delete application.householdMembers if _.isEmpty(application.householdMembers)
    delete application.primaryApplicant.mailingGeocoding_data
    delete application.completedSections
    delete application.validatedForms
    return application

  Service._renameApplicant = (application) ->
    application.primaryApplicant = angular.copy(application.applicant)
    delete application.applicant
    return application

  Service._formatDOB = (application) ->
    month = application.applicant.dob_month
    day = application.applicant.dob_day
    year = application.applicant.dob_year
    formattedDOB = year + '-' + month + '-' + day
    application.applicant.dob = formattedDOB
    delete application.applicant.dob_month
    delete application.applicant.dob_day
    delete application.applicant.dob_year
    return application

  Service._formatMemberDOB = (application) ->
    application.householdMembers.forEach( (member) ->
      month = member.dob_month
      day = member.dob_day
      year = member.dob_year
      formattedDOB = year + '-' + month + '-' + day
      member.dob = formattedDOB
      delete member.dob_day
      delete member.dob_month
      delete member.dob_year
      delete member.id
      return
    )
    return application


  Service._formatAddress = (application, person, addressType) ->
    delete application[person].geocoding_data
    return application unless application[person][addressType]

    if addressType == 'home_address'
      _.forEach application[person][addressType], (value, key) ->
        if !(key == 'address1' || key == 'address2')
          application[person][key] = value
        return
      application[person].address = application[person][addressType].address1 + application[person][addressType].address2
      delete application[person].confirmed_home_address

    else if addressType == 'mailing_address'
      _.forEach application[person][addressType], (value, key) ->
        if !(key == 'address1' || key == 'address2')
          newKey = 'mailing' + _.capitalize(key)
          application[person][newKey] = value
        return
      application[person].mailingAddress = application[person][addressType].address1
      address2 = application[person][addressType].address2
      application[person].mailingAddress += " " + address2 if address2

    delete application[person][addressType]
    return application

  Service._formatMemberAddress = (application) ->
    application.householdMembers.forEach( (member) ->
      if member.home_address
        _.forEach member.home_address, (value, key) ->
          if !(key == 'address1' || key == 'address2')
            member[key] = value
          return
        member.address = member.home_address.address1 + member.home_address.address2

      if member.hasSameAddressAsApplicant == 'Yes'
        member.hasSameAddressAsApplicant = true
      else
        member.hasSameAddressAsApplicant = false

      delete member.home_address
      delete member.confirmed_home_address
      delete member.geocoding_data
    )
    return application

  Service._formatPreferences = (application) ->
    preferences = [
        'displaced',
        'certOfPreference',
        'liveInSf',
        'workInSf',
        'neighborhoodResidence'
    ]

    allMembers = angular.copy(application.householdMembers)
    allMembers.push(application.applicant)

    preferences.forEach( (preference) ->
      memberName = application.preferences[preference + '_household_member']
      member = _.find(allMembers, (m) ->
        return memberName == (m.firstName + ' ' + m.lastName)
      )
      return unless member
      memberDOB = member.dob.replace(/\//g, '.')
      if preference == 'certOfPreference'
        preferenceName = preference + 'NatKey'
      else
        preferenceName = preference + 'PreferenceNatKey'
      application[preferenceName] = member.firstName + ',' + member.lastName + ',' + memberDOB
      delete application.preferences[preference + '_household_member']
    )

    if application.householdVouchersSubsidies == 'Yes'
      application.householdVouchersSubsidies = true
    else
      application.householdVouchersSubsidies = false
    delete application.householdVouchersSubsidies
    delete application.preferences
    return application

  Service._formatGenderOptions = (application) ->
    allGenders = ""
    _.forEach application.applicant.gender, (value, key) ->
      allGenders += (key + ";") if value
      return
    application.applicant.gender = allGenders
    return application

  Service._formatReferrals = (application) ->
    referrals = ""
    _.forEach application.applicant.referral, (value, key) ->
      referrals += (key + ";") if value
      return
    application.referral = referrals
    delete application.applicant.referral
    return application

  Service._formatTerms = (application) ->
    if application.applicant.terms.yes
      application.agreeToTerms = true
    else
      application.agreeToTerms = false
    delete application.applicant.terms
    return application

  Service._formatIncome = (application) ->
    incomeTimeframe = application.householdIncome.incomeTimeframe
    if incomeTimeframe == 'per_year'
      application.annualIncome = application.householdIncome.incomeTotal
    else if incomeTimeframe == 'per_month'
      application.monthlyIncome = application.householdIncome.incomeTotal
    delete application.householdIncome
    return application

  Service._formatBooleans = (application) ->
    ['workInSf', 'hiv'].forEach (field) ->
      if application.applicant[field] == 'Yes'
        application.applicant[field] = true
      else
        application.applicant[field] = false

    application.householdMembers.forEach( (member) ->
      if member.workInSf == 'Yes'
        member.workInSf = true
      else
        member.workInSf = false
    )
    return application

  Service.checkSurveyComplete = ->
    app = Service.applicant
    responses = [
      _.some(app.gender),
      app.ethnicity,
      app.race,
      app.hiv,
      _.some(app.referral),
    ]
    Service.application.surveyComplete = _.every(responses)

  return Service

############################################################################################
######################################## CONFIG ############################################
############################################################################################

ShortFormApplicationService.$inject = [
  '$localStorage', '$translate', '$http', 'Upload', 'uuid', 'ListingService'
]

angular
  .module('dahlia.services')
  .service('ShortFormApplicationService', ShortFormApplicationService)
